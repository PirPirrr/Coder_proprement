plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def apikeyPropertiesFile = rootProject.file("apikey.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    compileSdk 31

    signingConfigs {
        production {
            storePassword keystoreProperties['storePassword']
            keyPassword keystoreProperties['keyPassword']
            keyAlias keystoreProperties['keyAlias']
            storeFile file(keystoreProperties['storeFile'])
        }
    }

    defaultConfig {
        applicationId "com.example.coderproprement"
        minSdk 21
        targetSdk 31

        def buildNumber = Integer.parseInt(property("version.buildNumber"))
        def major = Integer.parseInt(property("version.major"))
        def minor = Integer.parseInt(property("version.minor"))
        def patch = Integer.parseInt(property("version.patch"))
        versionCode buildNumber
        versionName "${major}.${minor}.${patch}-b${buildNumber}"

        buildConfigField "String", "MARVEL_PRIVATE_API_KEY", apikeyProperties['privatekey']
        buildConfigField "String", "MARVEL_PUBLIC_API_KEY", apikeyProperties['publickey']

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.production
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }


    flavorDimensions "environment"
    productFlavors {
        development {
            dimension "environment"
            applicationIdSuffix = ".dev"
            versionNameSuffix '-dev'
            buildConfigField "String", "WSUrl", "\"https://gateway.marvel.com:443/\""
        }

        production {
            dimension "environment"
            applicationIdSuffix = ".prod"
            versionNameSuffix '-prod'
            buildConfigField "String", "WSUrl", "\"https://gateway.marvel.com:443/\""
        }
    }

    buildFeatures {
        viewBinding true
    }
    namespace 'com.example.coderproprement'
    buildToolsVersion '31.0.0'
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    api 'com.google.android.material:material:1.7.0-alpha01'
    implementation 'io.coil-kt:coil:1.1.0'


    androidTestImplementation("androidx.navigation:navigation-testing:2.4.2")
    implementation("androidx.navigation:navigation-dynamic-features-fragment:2.4.2")
    implementation("androidx.navigation:navigation-compose:2.4.2")

    implementation "io.insert-koin:koin-android:3.1.6"

    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation 'com.google.code.gson:gson:2.8.8'
}